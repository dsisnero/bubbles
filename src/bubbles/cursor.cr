require "../tea"
require "../lipgloss"
require "./context"
require "atomic"

module Bubbles
  module Cursor
    extend self

    DEFAULT_BLINK_SPEED = 530.milliseconds

    # Internal ID management. Used during animating to ensure that frame messages
    # are received only by spinner components that sent them.
    @@last_id = Atomic(Int64).new(0)

    def self.next_id : Int32
      (@@last_id.add(1) + 1).to_i32
    end

    # initial_blink_msg initializes cursor blinking.
    class InitialBlinkMsg < Tea::Msg
    end

    # BlinkMsg signals that the cursor should blink. It contains metadata that
    # allows us to tell if the blink message is the one we're expecting.
    class BlinkMsg < Tea::Msg
      property id : Int32
      property tag : Int32

      def initialize(@id, @tag)
      end
    end

    # blink_canceled is sent when a blink operation is canceled.
    class BlinkCanceled < Tea::Msg
    end

    # blink_ctx manages cursor blinking.
    class BlinkCtx
      property ctx : Context::Context
      property cancel : Context::CancelFunc?

      def initialize(@ctx : Context::Context)
      end
    end

    # Mode describes the behavior of the cursor.
    enum Mode
      Blink
      Static
      Hide
    end

    # Model is the Bubble Tea model for this cursor element.
    class Model
      # Style styles the cursor block.
      property style : Lipgloss::Style

      # TextStyle is the style used for the cursor when it is blinking
      # (hidden), i.e. displaying normal text.
      property text_style : Lipgloss::Style

      # BlinkSpeed is the speed at which the cursor blinks. This has no effect
      # unless [CursorMode] is not set to [CursorBlink].
      property blink_speed : Time::Span

      # IsBlinked is the state of the cursor blink. When true, the cursor is
      # hidden.
      property? is_blinked : Bool

      # char is the character under the cursor
      property char : String

      # The ID of this Model as it relates to other cursors
      property id : Int32

      # focus indicates whether the containing input is focused
      property? focus : Bool

      # Used to manage cursor blink
      property blink_ctx : BlinkCtx

      # The ID of the blink message we're expecting to receive.
      property blink_tag : Int32

      # mode determines the behavior of the cursor
      property mode : Mode

      # Creates a new model with default settings.
      def initialize
        @id = Cursor.next_id
        @blink_speed = DEFAULT_BLINK_SPEED
        @is_blinked = true
        @mode = Mode::Blink
        @style = Lipgloss::Style.new
        @text_style = Lipgloss::Style.new
        @char = ""
        @focus = false
        @blink_ctx = BlinkCtx.new(Context.background)
        @blink_tag = 0
      end

      # Update updates the cursor.
      def update(msg : Tea::Msg) : {Model, Tea::Cmd}
        case msg
        when InitialBlinkMsg
          # We accept all initialBlinkMsgs generated by the Blink command.
          if @mode != Mode::Blink || !@focus
            return {self, nil}
          end
          cmd = blink
          {self, cmd}
        when Tea::FocusMsg
          cmd = focus
          {self, cmd}
        when Tea::BlurMsg
          blur
          {self, nil}
        when BlinkMsg
          # We're choosy about whether to accept blinkMsgs so that our cursor
          # only exactly when it should.
          # Is this model blink-able?
          if @mode != Mode::Blink || !@focus
            return {self, nil}
          end
          # Were we expecting this blink message?
          if msg.id != @id || msg.tag != @blink_tag
            return {self, nil}
          end
          cmd = nil
          if @mode == Mode::Blink
            @is_blinked = !@is_blinked
            cmd = blink
          end
          {self, cmd}
        when BlinkCanceled
          {self, nil}
        else
          {self, nil}
        end
      end

      # Mode returns the model's cursor mode. For available cursor modes, see
      # type Mode.
      def mode : Mode
        @mode
      end

      # SetMode sets the model's cursor mode. This method returns a command.
      #
      # For available cursor modes, see type CursorMode.
      # ameba:disable Naming/AccessorMethodName
      def set_mode(mode : Mode) : Tea::Cmd
        # Adjust the mode value if it's value is out of range
        if mode.value < Mode::Blink.value || mode.value > Mode::Hide.value
          return
        end
        @mode = mode
        @is_blinked = @mode == Mode::Hide || !@focus
        if mode == Mode::Blink
          return -> { InitialBlinkMsg.new.as(Tea::Msg?) }.as(Tea::Cmd)
        end
        nil
      end

      # Blink is a command used to manage cursor blinking.
      def blink : Tea::Cmd
        if @mode != Mode::Blink
          return
        end
        @blink_ctx.cancel.try(&.call)
        ctx, cancel = Context.with_timeout(@blink_ctx.ctx, @blink_speed)
        @blink_ctx.ctx = ctx
        @blink_ctx.cancel = cancel
        @blink_tag += 1
        blink_msg = BlinkMsg.new(@id, @blink_tag)
        -> {
          begin
            ctx.done.receive?
            if ctx.err.is_a?(Context::Error::DeadlineExceeded)
              blink_msg.as(Tea::Msg?)
            else
              BlinkCanceled.new.as(Tea::Msg?)
            end
          ensure
            cancel.call
          end
        }.as(Tea::Cmd)
      end

      # Focus focuses the cursor to allow it to blink if desired.
      def focus : Tea::Cmd
        @focus = true
        @is_blinked = @mode == Mode::Hide # show the cursor unless we've explicitly hidden it
        if @mode == Mode::Blink && @focus
          return blink
        end
        nil
      end

      # Blur blurs the cursor.
      def blur
        @focus = false
        @is_blinked = true
      end

      # SetChar sets the character under the cursor.
      # ameba:disable Naming/AccessorMethodName
      def set_char(char : String)
        @char = char
      end

      # View displays the cursor.
      def view : String
        if @is_blinked
          @text_style.inline(true).render(@char)
        else
          @style.inline(true).reverse(true).render(@char)
        end
      end
    end

    # Blink is a command used to initialize cursor blinking.
    def self.blink : Tea::Msg
      InitialBlinkMsg.new
    end
  end
end
